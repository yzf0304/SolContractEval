```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.7;

contract Okimi is Ownable, ERC721A, ReentrancyGuard {
    string private _baseTokenURI;

    uint256 public immutable maxPerAddressDuringMint;
    uint256 public immutable amountForDevs;
    uint256 public immutable amountForSaleAndDev;
    uint256 internal immutable collectionSize;
    uint256 internal immutable maxBatchSize;

    struct SaleConfig {
        uint32 publicSaleStartTime;
        uint64 publicPriceWei;
    }

    SaleConfig public saleConfig;

    constructor() ERC721A("Okimi", "OKM") {
        maxPerAddressDuringMint = 5;
        maxBatchSize = 5;
        collectionSize = 1500;
        amountForDevs = 1500;
        amountForSaleAndDev = 1500;
        saleConfig.publicPriceWei = 0.005 ether;
        saleConfig.publicSaleStartTime = uint32(block.timestamp);
    }

    modifier callerIsUser() {
        require(tx.origin == msg.sender, "The caller is another contract");
        _;
    }

    function mint(uint256 quantity) external payable callerIsUser {
        SaleConfig memory config = saleConfig;
        uint256 publicPrice = uint256(config.publicPriceWei);
        uint256 publicSaleStartTime = uint256(config.publicSaleStartTime);
        require(isSaleOn(publicPrice, publicSaleStartTime), "Sale not active");
        require(totalSupply() + quantity <= collectionSize, "Exceeds max supply");
        require(numberMinted(msg.sender) + quantity <= maxPerAddressDuringMint, "Exceeds per address limit");
        require(quantity <= maxBatchSize, "Exceeds max batch size");

        _safeMint(msg.sender, quantity);
        refundIfOver(publicPrice * quantity);
    }

    function refundIfOver(uint256 price) private {
        require(msg.value >= price, "Need to send more ETH");
        if (msg.value > price) {
            payable(msg.sender).transfer(msg.value - price);
        }
    }

    function isSaleOn(uint256 _price, uint256 _startTime) public view returns (bool) {
        return _price != 0 && block.timestamp >= _startTime;
    }

    function getPrice() public view returns (uint256) {
        return saleConfig.publicPriceWei;
    }

    function setPublicSaleConfig(uint32 timestamp, uint64 price) external onlyOwner {
        saleConfig.publicSaleStartTime = timestamp;
        saleConfig.publicPriceWei = price;
    }

    function reserve(uint256 quantity) external onlyOwner {
        require(totalSupply() + quantity <= amountForDevs, "exceeds dev supply");
        uint256 numChunks = quantity / maxBatchSize;
        for (uint256 i = 0; i < numChunks; i++) {
            _safeMint(msg.sender, maxBatchSize);
        }
        uint256 remainder = quantity % maxBatchSize;
        if (remainder != 0) {
            _safeMint(msg.sender, remainder);
        }
    }

    function setBaseURI(string calldata baseURI) external onlyOwner {
        _baseTokenURI = baseURI;
    }

    function _baseURI() internal view override returns (string memory) {
        return _baseTokenURI;
    }

    function withdraw() external onlyOwner nonReentrant {
        (bool success, ) = msg.sender.call{value: address(this).balance}("");
        require(success, "Transfer failed");
    }

    function numberMinted(address owner) public view returns (uint256) {
        return _numberMinted(owner);
    }

    function getOwnershipData(uint256 tokenId) external view returns (TokenOwnership memory) {
        return _ownershipOf(tokenId);
    }

    function totalMinted() public view returns (uint256) {
        return _totalMinted();
    }

    receive() external payable {}
}
```